plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id("com.apollographql.apollo3") version "3.8.1"
}

def apikeyPropertiesFile = rootProject.file("api-key.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    namespace 'com.neowise.leads'
    compileSdk 33

    defaultConfig {
        applicationId "com.neowise.leads"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "SERVER_URL", apikeyProperties['SERVER_URL'])
        buildConfigField("String", "TOKEN", apikeyProperties['TOKEN'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

apollo {
    generateKotlinModels.set(true)

    service("service") {
        packageName.set("com.neowise.leads.graphql")
        mapScalar("DateTime", "java.util.Date", "com.apollographql.apollo3.adapter.DateAdapter")
    }
}

dependencies {
    def nav_version = "2.5.3"
    def binding_delegate_version = "1.5.9"
    def graphql_version = "3.8.1"
    def koin_android_version = "3.4.0"

    // DI
    implementation "io.insert-koin:koin-android:$koin_android_version"

    // GraphQL
    implementation "com.apollographql.apollo3:apollo-runtime:$graphql_version"
    implementation "com.apollographql.apollo3:apollo-adapters:$graphql_version"
    implementation("com.apollographql.apollo3:apollo-normalized-cache-sqlite:$graphql_version")
    // Components
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewBinding Delegate
    implementation "com.github.kirich1409:viewbindingpropertydelegate-full:$binding_delegate_version"
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:$binding_delegate_version"

    implementation 'androidx.emoji2:emoji2:1.3.0'
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}